plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'eclipse'
    id "com.jfrog.bintray" version '1.6'
}

repositories {
    jcenter()
}

ext {
	springBootVersion = '1.4.0.RELEASE'
	springRestdocsVersion = '1.1.2.RELEASE'
	wiremockVersion = '2.1.12'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = 'com.epages'
description = 'Spring Boot Starter for adding a WireMock server to your integration test'

dependencies {
    compile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
    compile "com.github.tomakehurst:wiremock:$wiremockVersion"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'wiremock-spring-boot-starter'
            groupId = project.group

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

bintray {
	user = project.findProperty('bintrayUser')   ?: System.getenv('BINTRAY_USER')
	key  = project.findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
	publications = ['mavenJava','sourceJar']
	publish = true
	pkg {
		repo = "maven"
		name = 'wiremock-spring-boot-starter'
		userOrg = 'epages'
		websiteUrl = 'https://github.com/ePages-de/restdocs-wiremock'
		issueTrackerUrl = 'https://github.com/ePages-de/restdocs-wiremock/issues'
		vcsUrl = 'https://github.com/ePages-de/restdocs-wiremock.git'
		githubRepo = 'ePages-de/restdocs-wiremock'
		githubReleaseNotesFile = 'README.md'
		licenses = ['Apache-2.0']
	}
}
