buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'pl.allegro.tech.build.axion-release' version '1.15.0'
    id 'publishing'
}

repositories {
    mavenCentral()
}

scmVersion {
    tag {
        prefix = ''
    }

    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v,p -> /($v)/}, replacement: {v, p -> "$v"}]
        pre 'commit'
    }
}

group = 'com.epages'

allprojects {
    project.version = scmVersion.version
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }
    test.finalizedBy jacocoTestReport
}

nexusPublishing {
    repositories {
        sonatype ()
    }
}

sonarqube {
  properties {
    property "sonar.projectKey", "ePages-de_restdocs-wiremock"
    property "sonar.organization", "epages-de"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
