plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    // FIXME: Starting with version 2.0.3.RELEASE there are missing dependencies from "org.springframework.web"
    // See https://github.com/spring-projects/spring-restdocs/releases/tag/v2.0.4.RELEASE
    implementation 'org.springframework.restdocs:spring-restdocs-core:2.0.3.RELEASE'
    implementation 'com.jayway.jsonpath:json-path:2.8.0'
    compileOnly 'junit:junit:4.13.1'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:2.18.0'
    testImplementation 'uk.co.datumedge:hamcrest-json:0.2'
    testImplementation 'com.google.guava:guava:30.0-jre'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

group = 'com.epages'
description = 'Adds WireMock stub snippets to Spring REST Docs'
sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'restdocs-wiremock'
            groupId = project.group

            pom {
                  name = 'REST Docs WireMock'
                  description = 'Spring REST Docs WireMock Integration'
                  url = 'https://github.com/ePages-de/restdocs-wiremock'
                  licenses {
                      license {
                          name = 'The Apache License, Version 2.0'
                          url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                      }
                  }
                  developers {
                      developer {
                          id = 'ePages'
                          name = 'ePages Devs'
                          email = 'info@epages.com'
                      }
                  }
                  scm {
                      connection = 'scm:git:git://github.com:ePages-de/restdocs-wiremock.git'
                      developerConnection = 'scm:git:ssh://github.com:ePages-de/restdocs-wiremock.git'
                      url = 'https://github.com/ePages-de/restdocs-wiremock'
                  }
            }
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
}
