buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.4.13'
    }
}

plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.epages'
description = 'Spring Boot Starter for adding a WireMock server to your integration test'

dependencies {
    api 'com.github.tomakehurst:wiremock-standalone:2.18.0'

    implementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.boot:spring-boot-starter'

    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'junit:junit'
    testImplementation 'org.springframework:spring-web'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

bootJar.enabled = false
jar.enabled = true

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'wiremock-spring-boot-starter'
            groupId = project.group
            
            pom {
                  name = 'WireMock Spring Boot Starter'
                  description = "A Spring Boot Starter which adds a WireMockServer to your client's ApplicationContext for integration testing."
                  url = 'https://github.com/ePages-de/restdocs-wiremock'
                  licenses {
                      license {
                          name = 'The Apache License, Version 2.0'
                          url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                      }
                  }
                  developers {
                      developer {
                          id = 'ePages'
                          name = 'ePages Devs'
                          email = 'info@epages.com'
                      }
                  }
                  scm {
                      connection = 'scm:git:git://github.com:ePages-de/restdocs-wiremock.git'
                      developerConnection = 'scm:git:ssh://github.com:ePages-de/restdocs-wiremock.git'
                      url = 'https://github.com/ePages-de/restdocs-wiremock'
                  }
            }
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
}
